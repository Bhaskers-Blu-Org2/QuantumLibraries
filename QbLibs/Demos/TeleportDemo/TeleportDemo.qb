namespace Microsoft.Quantum.Examples.TeleportDemo {
	open Microsoft.Quantum.Primitive

	operation Teleport (message : Bool) : Bool {
        Body {
			mutable measurement = false

            using (register = Qubit[3]) {
				// Ask for some qubits that we can use to teleport.
				let msg = register[0]
				let here = register[1]
				let there = register[2]
                
				// Encode the message we want to send.
				if (message) { X(msg) }
            
                // Create some entanglement that we can use to send our message.
                H(here)
                CNOT(here, there)
            
                // Move our message into the entangled pair.
                CNOT(msg, here)
                H(msg)

                // Measure out the entanglement.
                if (M(msg) == One)  { Z(there) }
				if (M(here) == One) { X(there) }

				// Check what message was sent.
				if (M(there) == One) { set measurement = true }
            }

			return measurement
		}
	}
}